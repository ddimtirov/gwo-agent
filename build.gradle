plugins {
    id 'maven-publish'
    id 'java-library'
    id "com.github.johnrengelman.shadow" version "4.0.0"
}

group = 'io.github.ddimtirov.gradle'
version = System.getenv('VERSION') ?: version

repositories.jcenter()
dependencies.implementation 'net.bytebuddy:byte-buddy:1.8.22'

targetCompatibility = 1.6
sourceCompatibility = 1.6
compileJava.options.compilerArgs.add('-Xlint:-options')

jar {
    manifest.attributes(
            'Premain-Class': 'io.github.ddimtirov.gradle.wrapperoverride.GradleWrapperOverrideAgent',
            'Can-Redefine-Classes': true,
            'Can-Retransform-Classes': true
    )
    manifest.attributes('io.github.ddimtirov.gradle.wrapperoverride',
            'Specification-Title': 'Gradle Wrapper Override Agent',
            'Specification-Version': version ?: 'UNKNOWN',
            'Specification-Vendor': 'http://github.com/ddimtirov/gwo-agent',
            'Implementation-Title': "$project.group:$project.name",
            'Implementation-Version': System.getenv('GIT_COMMIT') ?: 'UNKNOWN',
            'Implementation-Vendor': System.getenv('JITPACK') ? 'https://jitpack.io' : 'UNKNOWN'
    )
    enabled = false
}

shadowJar {
    classifier = null
    relocate 'net.bytebuddy', 'relocated.wrapperoverride.bytebuddy'
    minimize()
    preserveFileTimestamps = false
    assemble.dependsOn it
}

publishing{
    publications {
        agent(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}
